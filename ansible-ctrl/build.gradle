apply plugin: 'com.palantir.docker'

def findEnv(String defaultEnv) {
    findProperty('env') ?: defaultEnv
}

def findEnv() {
    findEnv('noenv')
}

import org.apache.tools.ant.filters.ReplaceTokens

def condaEnvName(prjName = project.getName(), env = findEnv()) {
    "${prjName}-${env}".toString()
}

def dockerImageName(def prefix) {
    "${prefix == null ? '' : prefix + '/'}${condaEnvName()}-builder:${project.getVersion()}".toString()
}

def dockerContainerName() {
    "${condaEnvName()}-builder-container".toString()
}

tasks.docker.dependsOn project(':ansible-conda-pack').tasks.dockerRun

docker {
    def dependenciesDir = 'image-dependencies'

    name "${dockerImageName('noamasor')}"
    // Files to be used while building image bu Dockerfile
    copySpec.with {
        into dependenciesDir
        from project(':ansible-conda-pack').tasks.dockerRun.outputs.files.singleFile
        from('src/main/resources') {
            filter(ReplaceTokens, tokens: [PROXY_USER : findProperty('artifactoryUser') ?: 'user',
                                           PROXY_PASS : findProperty('artifactoryPass') ?: 'pass',
                                           condaEnvName : condaEnvName()])
        }
    }

    buildArgs([DEPENDENCIES_DIR: "$dependenciesDir",
               BUILD_DIR    : '/tmp/generated',
               CONDA_ENV_NAME : condaEnvName(project(':ansible-conda-pack').name),
               VERSION : project.getVersion(),
               PROXY_USER : findProperty('artifactoryUser') ?: 'user',
               PROXY_PASS : findProperty('artifactoryPass') ?: 'pass'
    ])

    noCache true
}

tasks.docker.configure {
    inputs.dir('src/main/resources')
    inputs.file('Dockerfile')
}

project.pluginManager.withPlugin('com.palantir.docker-run') {
    if (!file(layout.buildDirectory.dir("generated")).exists()) {
        mkdir layout.buildDirectory.dir("generated")
    }
}

apply plugin: 'com.palantir.docker-run'

dockerRun {
    name "${dockerContainerName()}"
    image "${dockerImageName('noamasor')}"
    volumes "${buildDir}/generated": '/tmp/generated/out'
    daemonize false
    command '/bin/bash', '-c', "cat /home/ansible/.ssh/id_rsa.pub > /tmp/generated/out/id_rsa.pub"
    // plugin v0.26.0 arguments feature has bug
    //arguments "${project.version}"
    //env 'IMG_VERSION': "${project.version}"
}

tasks.dockerRun.configure {
    inputs.dir('src/main/resources')
    inputs.file('Dockerfile')
    outputs.dir("${buildDir}/generated")
}

tasks.dockerRun.dependsOn tasks.docker

def prepare = tasks.register('prepare') {
    dependsOn 'dockerRun'
}
