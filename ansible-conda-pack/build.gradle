// group = 'com.github.prokod'
// version = '0.0.1'

apply plugin: 'com.palantir.docker'

def findEnv(String defaultEnv) {
    findProperty('env') ?: defaultEnv
}

def findEnv() {
    findEnv('noenv')
}

import org.apache.tools.ant.filters.ReplaceTokens

def condaEnvName() {
    "${project.getName()}-${findEnv()}".toString()
}

def dockerImageName(def prefix) {
    "${prefix == null ? '' : prefix + '/'}${condaEnvName()}-builder:${project.getVersion()}".toString()
}

def dockerContainerName() {
    "${condaEnvName()}-builder-container".toString()
}

def currentUser() {
    System.getenv("USER")
}

docker {
    def dependenciesDir = 'image-dependencies'

    name "${dockerImageName('noamasor')}"
    // Files to be used while building image bu Dockerfile
    copySpec.with {
        from('src') {
            filter(ReplaceTokens, tokens: [PROXY_USER : findProperty('artifactoryUser') ?: 'user',
                                           PROXY_PASS : findProperty('artifactoryPass') ?: 'pass',
                                           condaEnvName : condaEnvName()])
            into dependenciesDir
        }
    }

    buildArgs([DEPENDENCIES_DIR: "$dependenciesDir",
               BUILD_DIR    : '/tmp/conda',
               CONDA_ENV_NAME : condaEnvName(),
               VERSION : project.getVersion(),
               PROXY_USER : findProperty('artifactoryUser') ?: 'user',
               PROXY_PASS : findProperty('artifactoryPass') ?: 'pass'
    ])

    noCache true
}

project.pluginManager.withPlugin('com.palantir.docker-run') {
    if (!file(project.buildDir).exists()) {
        mkdir project.buildDir
    }
}

apply plugin: 'com.palantir.docker-run'

dockerRun {
    name "${dockerContainerName()}"
    image "${dockerImageName('noamasor')}"
    volumes "${buildDir}": '/tmp/conda/out'
    daemonize false
    //command 'sleep', '100'
    // plugin v0.26.0 arguments feature has bug
    //arguments "${project.version}"
    env 'IMG_VERSION': "${project.version}"
}

def condaPack = tasks.register('condaPack') {
    dependsOn 'dockerRun'

    outputs.file("${buildDir}/${condaEnvName()}-${project.version}.tar.gz")
}
